plugins {
    id 'java'
    id 'maven-publish'
}

group 'dev.harrel'
version '0.2.2'

java {
    withSourcesJar()
    withJavadocJar()
    registerFeature('jackson') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('gson') {
        usingSourceSet(sourceSets.main)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    jacksonImplementation 'com.fasterxml.jackson.core:jackson-databind:2.14.1'
    gsonImplementation 'com.google.code.gson:gson:2.10.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

ext.reposiliteSnapshots = findProperty('reposiliteSnapshots') ?: 'not found'
ext.reposiliteUsername = findProperty('reposiliteUsername') ?: 'not found'
ext.reposilitePassword = findProperty('reposilitePassword') ?: 'not found'

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = 'json-schema'
                description = 'Library for JSON schema validation'
                url = 'https://github.com/harrel56/json-schema'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/harrel56/json-schema/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'harrel56'
                        name = 'Harrel'
                        url = 'https://harrel.dev'
                    }
                }
                scm {
                    url = 'https://github.com/harrel56/json-schema'
                    connection = 'scm:git:git://github.com/harrel56/json-schema.git'
                    developerConnection = 'scm:git:git://github.com/harrel56/json-schema.git'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/harrel56/json-schema/issues'
                }
            }
        }
    }
    repositories {
        maven {
            url = "$reposiliteSnapshots"
            credentials {
                username = "$reposiliteUsername"
                password = "$reposilitePassword"
            }
        }
    }
}